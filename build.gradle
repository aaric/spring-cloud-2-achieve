buildscript {
    ext {
        lombokVersion = "4.1.6"
        junitPlatformVersion = "1.2.0"
        dependencyManagementVersion = "1.0.9.RELEASE"
        springBootVersion = "2.1.13.RELEASE"
        springCloudVersion = "Greenwich.SR5"
    }

    repositories {
        maven { url "https://plugins.gradle.org/m2/" }
        maven { url "https://maven.aliyun.com/repository/public" }
    }

    dependencies {
        classpath "io.freefair.gradle:lombok-plugin:$lombokVersion"
        classpath "org.junit.platform:junit-platform-gradle-plugin:$junitPlatformVersion"
        classpath "io.spring.gradle:dependency-management-plugin:$dependencyManagementVersion"
        classpath "org.springframework.boot:spring-boot-gradle-plugin:$springBootVersion"
    }
}

allprojects {
    group "com.incarcloud"
    version "0.8.2-SNAPSHOT"

    apply plugin: "java"
    apply plugin: "io.spring.dependency-management"

    sourceCompatibility = JavaVersion.VERSION_11
    targetCompatibility = JavaVersion.VERSION_11

    repositories {
        maven { url "https://maven.aliyun.com/repository/public" }
        maven { url "https://repository.incarcloud.com/content/groups/public" }
    }

    configurations {
        developmentOnly
        runtimeClasspath {
            extendsFrom developmentOnly
        }
        /*all {
            exclude group: "org.springframework.boot", module: "spring-boot-starter-logging"
        }*/
    }

    dependencyManagement {
        imports {
            mavenBom "org.springframework.boot:spring-boot-dependencies:$springBootVersion"
            mavenBom "org.springframework.cloud:spring-cloud-dependencies:$springCloudVersion"
        }

        dependencies {
            dependencySet(group: "org.slf4j", version: "1.7.30") {
                entry "slf4j-api"
                entry "slf4j-simple"
            }

            dependencySet(group: "com.baomidou", version: "3.3.1") {
                entry "mybatis-plus-core"
                entry "mybatis-plus-annotation"
                entry "mybatis-plus-boot-starter"
            }

            // 解决方法：junit5与spring-boot-dependencies版本一起发布
            /*dependency "org.junit.jupiter:junit-jupiter-api:5.3.2"
            dependency "org.junit.jupiter:junit-jupiter-engine:5.3.2"*/

            // 解决方法：security与spring-boot-dependencies版本一起发布
            /*dependency "org.springframework.security:spring-security-core:5.1.7.RELEASE"*/

            dependency "org.flywaydb:flyway-core:5.2.4"

            dependency "org.apache.commons:commons-io:1.3.2"
            dependency "org.apache.commons:commons-lang3:3.9"
            dependency "com.google.guava:guava:28.2-jre"

            dependency "javax.xml.bind:jaxb-api:2.3.1"
            dependency "io.swagger:swagger-annotations:1.5.20"
            dependency "io.springfox:springfox-swagger2:2.8.0"
            dependency "com.github.xiaoymin:swagger-bootstrap-ui:1.9.6"

            dependency "com.zaxxer:HikariCP:3.4.2"
            dependency "mysql:mysql-connector-java:8.0.18"

            dependency "javax.validation:validation-api:2.0.1.Final"
        }
    }
}

subprojects {
    apply plugin: "checkstyle"
    apply plugin: "io.freefair.lombok"

    dependencies {
        compile "org.slf4j:slf4j-api"

        testCompile "org.junit.jupiter:junit-jupiter-api"
        testRuntimeOnly "org.junit.jupiter:junit-jupiter-engine"
        testCompile("org.springframework.boot:spring-boot-starter-test") {
            exclude group: "junit", module: "junit"
            exclude group: "org.junit.vintage", module: "junit-vintage-engine"
        }

        developmentOnly "org.springframework.boot:spring-boot-devtools" //支持热部署依赖
    }

    compileJava {
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xlint:unchecked"
    }

    compileTestJava {
        options.encoding = "UTF-8"
        options.compilerArgs << "-Xlint:unchecked"
    }

    test {
        useJUnitPlatform()
    }

    task checkstyle(type: Checkstyle) {
        configFile file("${rootDir}/config/checkstyle/checkstyle.xml")
        ignoreFailures true
        showViolations true
        source = fileTree("src")
        include "**/*.java"
        exclude "**/test/**"
        exclude "**/build/**"
        classpath = files()
    }

    task removeLombokConfig() {
        def subProjectPath = project.projectDir
        if (null != subProjectPath) {
            def lombokConfigFile = new File(subProjectPath, "lombok.config")
            if (lombokConfigFile.exists()) {
                lombokConfigFile.delete()
            }
        }
    }

    defaultTasks "assemble"
}
